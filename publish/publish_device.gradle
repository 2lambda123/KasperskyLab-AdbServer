ext {
    ver = '1.0.0'
    groupId = 'com.kaspersky.android-components'
    artifactId = 'adbserver-device'
    repo = 'AdbServer'
    repoName = 'adbserver-device'
    vcsUrl = 'https://github.com/KasperskyLab/AdbServer.git'
}

task fatJar(type: Jar) {
    def included = [
            'common.jar',
            'command_types.jar',
            'connection.jar',
            'desktop_device_connection.jar'
    ]

    from {
        configurations.runtimeClasspath
                .filter { included.contains(it.name) }
                .collect { it.isDirectory() ? it : zipTree(it) }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}


publishing {
    publications {
        Device(MavenPublication) {
            artifact fatJar
            artifact sourcesJar
            artifact javadocJar
            groupId this.groupId
            artifactId this.artifactId
            version this.ver

            pom.withXml {
                def dependenciesNode = asNode().getAt('dependencies')[0]
                        ?: asNode().appendNode('dependencies')

                configurations.implementation.allDependencies.each {
                    if (it.name != 'unspecified' && it.version != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = property('bintrayuser')
    key = property('bintraykey')
    publications = ['Device']

    pkg {
        repo = this.repo
        name = this.repoName
        userOrg = user
        licenses = ['Apache-2.0']
        vcsUrl = this.vcsUrl

        version {
            name = this.ver
            released = new Date()
        }
    }
}
