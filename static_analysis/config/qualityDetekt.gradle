task detektAll()

// Note: don't forget to update detekt version in main build gradle file
def detektVersion = "1.0.0-RC14"
def analysisDir = "${project.rootDir}/static_analysis"
def reportsDir = "$analysisDir/reports"

subprojects { subproject ->
    apply plugin: 'io.gitlab.arturbosch.detekt'

    configurations {
        detekt
    }

    dependencies {
        detekt("io.gitlab.arturbosch.detekt:detekt-formatting:${detektVersion}")
        detekt("io.gitlab.arturbosch.detekt:detekt-cli:${detektVersion}")
    }

    detekt {
        toolVersion = "$detektVersion"
        input = files("src")
        filters = ".*/resources/.*,.*/build/.*"
        config = files("${project.rootDir}/static_analysis/config/detekt/config.yml")

        reports {
            html {
                destination = file("$reportsDir/html/detekt_${subproject.name}.html")
            }
        }
    }
    detektAll.dependsOn tasks.detekt
}
